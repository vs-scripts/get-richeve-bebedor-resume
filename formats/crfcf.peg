# ================================================================================
# Terminal Components
# ================================================================================

SP                      <- " "
LF                      <- "\n"
EOL                     <- LF


# ================================================================================
# Character Components
# ================================================================================

ALPHA                   <- [A-Za-z]
DIGIT                   <- [0-9]
ALPHANUMERIC            <- ALPHA / DIGIT

HYPHEN                  <- "-"
UNDERSCORE              <- "_"
DOT                     <- "."
COMMA                   <- ","
COLON                   <- ":"
SEMICOLON               <- ";"
SLASH                   <- "/"
BSLASH                  <- "\\"
AT                      <- "@"
PLUS                    <- "+"


# ================================================================================
# Indentation Components
# ================================================================================

IndentUnit              <- SP SP SP SP
Indent                  <- IndentUnit


# ================================================================================
# Word Components
# ================================================================================

AlphanumericWord        <- ALPHANUMERIC+
NumericWord             <- DIGIT+

# --------------------------------------------------------------------------------
# Separator-Based Word Components
# --------------------------------------------------------------------------------

HyphenWord              <- AlphanumericWord (HYPHEN AlphanumericWord)*
UnderscoreWord          <- AlphanumericWord (UNDERSCORE AlphanumericWord)*
DotWord                 <- AlphanumericWord (DOT AlphanumericWord)*
ColonWord               <- AlphanumericWord (COLON AlphanumericWord)*
SlashWord               <- AlphanumericWord (SLASH AlphanumericWord)*
BackslashWord           <- AlphanumericWord (BSLASH AlphanumericWord)*

# --------------------------------------------------------------------------------
# URI Word Components
# --------------------------------------------------------------------------------

Scheme                  <- ALPHA (ALPHA / DIGIT / PLUS / HYPHEN / DOT)*
Authority               <- AlphanumericWord (DOT AlphanumericWord)*
Path                    <- SlashWord

UrlWord                 <- Scheme COLON SLASH SLASH Authority (SLASH Path)?

# --------------------------------------------------------------------------------
# Email Word Components
# --------------------------------------------------------------------------------

EmailSeparator          <- DOT / HYPHEN
EmailToken              <- AlphanumericWord (EmailSeparator AlphanumericWord)*
EmailSubaddress         <- PLUS EmailToken
EmailPart               <- EmailToken
EmailDomain             <- DotWord

EmailWord               <- EmailPart EmailSubaddress? AT EmailDomain

# --------------------------------------------------------------------------------
# Word Set Components
# --------------------------------------------------------------------------------

WordPart                <- EmailWord
                         / UrlWord
                         / SlashWord
                         / BackslashWord
                         / ColonWord
                         / DotWord
                         / HyphenWord
                         / UnderscoreWord
                         / NumericWord
                         / AlphanumericWord

QuotedWordPart          <- "'" WordPart "'"
                         / "\"" WordPart "\""

ContainedWordPart       <- "[" WordPart "]"
                         / "<" WordPart ">"
                         / "(" WordPart ")"

Word                    <- QuotedWordPart
                         / ContainedWordPart
                         / WordPart


# ================================================================================
# Lexical & Linguistic Components
# ================================================================================

PhraseSeparator         <- COMMA / SEMICOLON

Phrase                  <- Word (PhraseSeparator? SentenceSeparator Word)*

Sentence                <- Phrase (SentenceSeparator Phrase)* DOT

SentenceSeparator       <- SP / EOL

ParagraphPart           <- Sentence (SentenceSeparator Sentence)*

Level0Paragraph         <- ParagraphPart
Level1Paragraph         <- Indent ParagraphPart
Level2Paragraph         <- Indent Indent ParagraphPart
Level3Paragraph         <- Indent Indent Indent ParagraphPart
Level4Paragraph         <- Indent Indent Indent Indent ParagraphPart
Level5Paragraph         <- Indent Indent Indent Indent Indent ParagraphPart

Paragraph               <- Level0Paragraph
                         / Level1Paragraph
                         / Level2Paragraph
                         / Level3Paragraph
                         / Level4Paragraph
                         / Level5Paragraph


# ================================================================================
# Key-Value Components
# ================================================================================

Key                     <- Phrase COLON
KeyValue                <- Key SP Phrase


# ================================================================================
# List Components
# ================================================================================

ListItem                <- KeyValue
                         / Key
                         / Phrase

Level1Order             <- ALPHANUMERIC

Level2Order             <- ALPHANUMERIC DOT ALPHANUMERIC

OrderNumber             <- Level1Order
                         / Level2Order

Level1OrderItem         <- Level1Order DOT SP SP ListItem

Level2OrderItem         <- Level2Order SP ListItem

OrderedItem             <- Level1OrderItem
                         / Level2OrderItem

UnorderedItemPart       <- HYPHEN SP ListItem

Level0UnorderedItem     <- UnorderedItemPart
Level1UnorderedItem     <- Indent UnorderedItemPart
Level2UnorderedItem     <- Indent Indent UnorderedItemPart
Level3UnorderedItem     <- Indent Indent Indent UnorderedItemPart
Level4UnorderedItem     <- Indent Indent Indent Indent UnorderedItemPart
Level5UnorderedItem     <- Indent Indent Indent Indent Indent UnorderedItemPart

UnorderedItem           <- Level0UnorderedItem
                         / Level1UnorderedItem
                         / Level2UnorderedItem
                         / Level3UnorderedItem
                         / Level4UnorderedItem
                         / Level5UnorderedItem

OrderedList             <- (OrderedItem EOL)+
UnorderedList           <- (UnorderedItem EOL)+
ParagraphList           <- (Paragraph EOL)+


# ================================================================================
# Content-Block Components
# ================================================================================

Content                 <- ParagraphList
                         / OrderedList
                         / UnorderedList

Level0Block             <- Content
Level1Block             <- Indent Content
Level2Block             <- Indent Indent Content
Level3Block             <- Indent Indent Indent Content
Level4Block             <- Indent Indent Indent Indent Content
Level5Block             <- Indent Indent Indent Indent Indent Content

Block                   <- Level0Block
                         / Level1Block
                         / Level2Block
                         / Level3Block
                         / Level4Block
                         / Level5Block


# ================================================================================
# Section Components
# ================================================================================

MainSectionHeader       <- Level1Order DOT SP SP Key EOL

SubsectionHeader        <- Level2Order SP Key EOL

SpecificSectionHeader   <- Indent+ HYPHEN SP Key EOL

SectionHeader           <- MainSectionHeader
                         / SubsectionHeader

MainSectionBody         <- Level1Block

SupportSectionBody      <- Level1Block

SpecificSectionBody     <- Level1Block
                         / Level2Block
                         / Level3Block
                         / Level4Block
                         / Level5Block

SectionBody             <- MainSectionBody
                         / SupportSectionBody
                         / SpecificSectionBody

MainSection             <- MainSectionHeader
                           EOL
                           MainSectionBody

Subsection              <- SubsectionHeader
                           EOL
                           SupportSectionBody

SpecificSection         <- SpecificSectionHeader
                           EOL
                           SpecificSectionBody

Section                 <- MainSection
                         / Subsection
                         / SpecificSection

SectionList             <- (Section EOL)+


# ================================================================================
# Document Markers
# ================================================================================

MarkerLine              <- "<" "-" "-" "-" "-" "-" "-" "-" "-" "-" "-"
                           "-" "-" "-" "-" "-" "-" "-" "-" "-" "-" "-"
                           "-" "-" "-" "-" "-" "-" "-" "-" "-" "-" ">"

BeginTag                <- "[" SP "BEGIN-CRFCF" SP "]"

EndedTag                <- "[" SP "ENDED-CRFCF" SP "]"

BeginMarker             <- "|" MarkerLine BeginTag MarkerLine "|" EOL

EndedMarker             <- "|" MarkerLine EndedTag MarkerLine "|" EOL


# ================================================================================
# Document Structure
# ================================================================================

Document                <- BeginMarker
                           Disclaimer
                           SectionList
                           FooterNotes?
                           EndedMarker

Disclaimer              <- Paragraph
FooterNotes             <- Level0Block
