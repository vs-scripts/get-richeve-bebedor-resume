; =================================================================================
; Core Terminals
; =================================================================================

SP                  = %x20

LF                  = %x0A

EOL                 = LF

ALPHA               = %x41-5A / %x61-7A

DIGIT               = %x30-39

; =================================================================================
; Document Structure
; =================================================================================

document            = begin-marker
                      disclaimer
                      1*(section-list)
                      end-marker

disclaimer          = paragraph

; =================================================================================
; Document Markers
; =================================================================================

marker-line         = "<" 31"-" ">"

begin-tag           = "[" SP "BEGIN-CRFCF" SP "]"

begin-marker        = "|" marker-line begin-tag marker-line "|" EOL

end-tag             = "[" SP "END-CRFCF" SP "]"

end-marker          = "|" marker-line end-tag marker-line "|" EOL

; =================================================================================
; Indentation Components
; =================================================================================

indent-unit         = 2*4SP

indent              = 1*indent-unit

; =================================================================================
; Document Components
; =================================================================================

word-character      = %x21-7E

word                = 1*(word-character)

phrase              = word *(SP word)

sentence            = phrase *(SP phrase) "."

sentence-separator  = SP / EOL

paragraph           = sentence *(sentence-separator sentence)

key                 = phrase ":"

key-value           = key SP phrase

content             = paragraph
                    / phrase
                    / key
                    / key-value

list-item           = 1*(content)

order-number        = 1(DIGIT) *1("." 1(DIGIT))

ordered-item        = order-number "." 1*2SP list-item

unordered-item      = "-" 1SP list-item

paragraph-list      = 1*(paragraph EOL)

ordered-list        = 1*(ordered-item EOL)

unordered-list      = 1*(unordered-item EOL)

content-list        = paragraph-list
                    / ordered-list
                    / unordered-list

block               = *indent content-list

; =================================================================================
; Section Components
; =================================================================================

section-header      = order-number "." 1*2SP key EOL

section-body        = 1*block

section             = section-header
                      EOL
                      section-body

section-list        = 1*(section EOL)
